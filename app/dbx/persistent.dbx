// dbx.v1 golang persistent.dbx .

model gerrit_user (
    key gerrit_username

    field gerrit_username text
    field chat_id         text

    // utimestamp because of sqlite :(
    field last_report     utimestamp (updatable, nullable)

    index (
        name last_report_idx
        fields last_report
    )
)

read one (
    select gerrit_user
    where  gerrit_user.gerrit_username = ?
)

read all (
    select gerrit_user
    where  gerrit_user.last_report < ?
)

update gerrit_user (
    where gerrit_user.gerrit_username = ?
    noreturn
)

create gerrit_user ( noreturn )

model inline_comment (
    key comment_id

    field comment_id text
    field updated_at utimestamp (updatable)

    index (
        name updated_at_idx
        fields updated_at
    )
)

update inline_comment (
    where inline_comment.comment_id = ?
)

delete inline_comment (
    where inline_comment.updated_at < ?
)

model team_config (
    key config_key

    field config_key text
    field config_value text (updatable)
)

create team_config ( noreturn )

read one (
    select team_config.config_value
    where  team_config.config_key = ?
)

update team_config (
    where team_config.config_key = ?
    noreturn
)

model patchset_announcement (
    key num

    // the num field serves no purpose other than to appease dbx, which requires a primary key
    field num serial

    field project_name text
    field change_num int
    field patchset_num int
    field message_handle text
    field ts utimestamp ( autoinsert )

    index (
        name patchset_announcement_project_name_change_num_patchset_num_idx
        fields project_name change_num patchset_num
    )
    index (
        name patchset_announcement_ts_idx
        fields ts
    )
)

create patchset_announcement ( noreturn )

read all (
    select patchset_announcement.message_handle
    where  patchset_announcement.project_name = ?
    where  patchset_announcement.change_num = ?
    where  patchset_announcement.patchset_num = ?
)

delete patchset_announcement (
    where patchset_announcement.ts < ?
)